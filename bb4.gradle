// Copyright by Barry G. Becker, 2013. Licensed under MIT License: http://www.opensource.org/licenses/MIT

/**
 * This is common gradle build configuration for all bb4 projects.
 * Any project on github can include it using
 * apply from: 'https://raw.github.com/barrybecker4/bb4-common/master/bb4.gradle'
 */
apply plugin: 'java'
apply plugin: 'scala'
sourceCompatibility = '1.7'
targetCompatibility = '1.7'
apply plugin: 'project-report'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
defaultTasks 'build'

/**
 * Secret credentials for Sonatype and signing
 * should be defined in ~/.gradle/gradle.properties if you need them.
 * Only sign the artifacts if it is a final release.
 */
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
if (!hasProperty('sonatypeUsername')) {
	ext.sonatypeUsername = ''
}
if (!hasProperty('sonatypePassword')) {
	ext.sonatypePassword = ''
}

sourceSets.main.scala.srcDirs = ["src/main/scala", "source"]
sourceSets.main.java.srcDirs = []
sourceSets.test.scala.srcDirs = ["src/test/scala", "test"]
sourceSets.test.java.srcDirs = []

/** all bb4 projects use this directory layout. *
sourceSets {
    main {
        java {
            srcDir 'source'
        }
        scala {
            srcDirs = ['src/main/scala']
        }
        resources {
            srcDir 'source'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        scala {
            srcDirs = ['src/test/scala']
        }
        resources {
            srcDir 'test'
        }
    }
} */


repositories {
    mavenCentral()

    // this is where bb4 release artifacts will come from
    maven { url "http://repo1.maven.org/maven2/" }
    // this is where SNAPSHOT releases will come from
    maven { url "https://oss.sonatype.org/content/groups/staging" }
    // some thirdparty jars from here
    maven { url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases" }
}

/** check for updates every build */
configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {
    compile 'java3d:vecmath:1.3.1'
    testCompile 'junit:junit:4.11'
}

task copyToLib(dependsOn: jar, type: Copy) {
    description = "Copies the downloaded dependencies to the lib directory so they can be deployed"
    into "build/libs"
    from configurations.runtime
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}